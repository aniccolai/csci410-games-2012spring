//Lauren Aberle
//This class incorporates all the territories and players in the game

class Board {
	field Array territories;
	field int numTerritories;
	field Player p1, p2;
	
	constructor Board new(Player pOne, Player pTwo) {
		let numTerritories = Constants.numTerritories();
		let p1 = pOne;
		let p2 = pTwo;
		do populateTerritories();
		return this;
	}
	
	//Creates an array of territories
	method void populateTerritories() {
		var Territory temp;
		var Player player;
		var int i;
		let territories = Array.new(numTerritories);
		//x, y, player, name, output line, output column, number of armies
		let temp = Territory.new(5, 7, p2, "A", 2, 2, 5);
		let territories[0] = temp;
		let temp = Territory.new(55, 7, p1, "B", 2, 8, 2);
		let territories[1] = temp;
		let temp = Territory.new(105, 7, p1, "C", 2, 14, 3);
		let territories[2] = temp;
		let temp = Territory.new(30, 55, p2, "D", 6, 5, 2);
		let territories[3] = temp;
		let temp = Territory.new(80, 55, p2, "E", 6, 11, 4);
		let territories[4] = temp;
		let temp = Territory.new(55, 103, p1, "F", 11, 8, 6);
		let territories[5] = temp;
		let temp = Territory.new(215, 7, p1, "G", 2, 28, 4);
		let territories[6] = temp;
		let temp = Territory.new(265, 7, p2, "H", 2, 34, 2);
		let territories[7] = temp;
		let temp = Territory.new(215, 55, p1, "I", 6, 28, 2);
		let territories[8] = temp;
		let temp = Territory.new(265, 55, p2, "J", 6, 34, 4);
		let territories[9] = temp;
		let i = 0;
		while (i < numTerritories) {
			let temp = territories[i];
			let player = temp.getPlayer();
			do player.setTerritory(0, true);
			do player.addTerritories(1);
			let i = i + 1;
		}
		//TODO: Initialize neighbor arrays for each territory
		//TODO: Draw lines between territories (time permitting)
		return;
	}
	
	//Draws every territory on the board
	method void drawBoard() {
		var int i;
		var Territory temp;
		do Screen.clearScreen();
		let i = 0;
		while (i < numTerritories) {
			let temp = territories[i];
			do temp.draw();
			let i = i + 1;
		}
		do Screen.drawLine(145, 27, 215, 27);
		return;
	}
	
	//Returns territory at given index
	method Territory getTerritory(int i) {
		return territories[i];
	}
	
	//Disposes of member fields
	method void dispose() {
		do territories.dispose();
		do p1.dispose();
		do p2.dispose();
		return;
	}
}