// Lauren Aberle
// This class encapsulates the general flow of the game

class RiskGame {
	field Board board;
	field Player p1, p2;
	
	constructor RiskGame new() {
		let p1 = Player.new(1);
		let p2 = Player.new(2);
		let board = Board.new(p1, p2);
		return this;
	}
	
	//Takes care of general game flow
	method void runGame() {
		var int winner;
		do displaySplashScreen();
		do board.drawBoard();
		let winner = 0;
		while (winner = 0) {
			let winner = playerTurn(p1);
			if (~(winner = 1)) { //Player 1 didn't just win
				let winner = playerTurn(p2);
			}
		}
		do displayWinner(winner);
		do cleanup();
		return;
	}
	
	//Player turn involves placing armies and then playing turn (attacking)
	//This method returns 1 if player 1 has won and 2 if player 2 has one
	// and returns 0 otherwise
	method int playerTurn(Player p) {
		var int oppTerr;
		do p.placeArmies(board);
		if (p = p1) {
			do p.playTurn(p2, board);
			let oppTerr = p2.getTerritoriesOwned();
			if( oppTerr = 0) {
				return 1;
			}
		}
		else { //If p = p2
			do p.playTurn(p1, board);
			let oppTerr = p1.getTerritoriesOwned();
			if (oppTerr = 0) {
				return 2;
			}
		}
		return 0;
	}
	
	//Displays end game screen with winner
	method void displayWinner(int winner) {
		do Screen.clearScreen();
		do Output.moveCursor(0, 0);
		do Output.printString(" _______     _____   ______   ___  ____ ");
		do Output.println();
		do Output.printString("|_   __ \\   |_   _|.' ____ \\ |_  ||_  _|  ");
		do Output.println();
		do Output.printString("  | |__) |    | |  | (___ \\_|  | |_/ /  ");
		do Output.println();
		do Output.printString("  |  __ /     | |   _.____`.   |  __'. ");
		do Output.println();
		do Output.printString(" _| |  \\ \\_  _| |_ | \\____) | _| |  \\ \\_ ");
		do Output.println();
		do Output.printString("|____| |___||_____| \\______.'|____||____| ");
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.printString("Congratulations Player ");
		do Output.printInt(winner);
		do Output.printString("! You win!");
		return;
	}
	
	//Displays beginning splash screen
	method void displaySplashScreen() {
		do Screen.clearScreen();
		do Output.moveCursor(0, 0);
		do Output.printString(" _______     _____   ______   ___  ____ ");
		do Output.println();
		do Output.printString("|_   __ \\   |_   _|.' ____ \\ |_  ||_  _|  ");
		do Output.println();
		do Output.printString("  | |__) |    | |  | (___ \\_|  | |_/ /  ");
		do Output.println();
		do Output.printString("  |  __ /     | |   _.____`.   |  __'. ");
		do Output.println();
		do Output.printString(" _| |  \\ \\_  _| |_ | \\____) | _| |  \\ \\_ ");
		do Output.println();
		do Output.printString("|____| |___||_____| \\______.'|____||____| ");
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.printString("Welcome to RISK!");
		do Output.println();
		do Output.printString("Player 1 holds white territories.");
		do Output.println();
		do Output.printString("Player 2 holds black territories.");
		do Output.println();
		do Output.printString("Press any key to begin!");
		do Keyboard.readChar();
		return;
	}
	
	//Cleans up objects
	method void cleanup() {
		do board.dispose();
		do p1.dispose();
		do p2.dispose();
		return;
	}

}