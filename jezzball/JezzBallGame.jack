class JezzBallGame
{
	field Level level;
	field int level_num;

	constructor JezzBallGame new() {
		return this;
	}

	method void play() {
		var int key;

		do Output.println();
		do Output.println();
		do Output.println();
		do Output.printString("               ___              ______       _ _ ");
		do Output.println();
		do Output.printString("              |_  |             | ___ \\     | | |");
		do Output.println();
		do Output.printString("                | | ___ ________| |_/ / __ _| | |");
		do Output.println();
		do Output.printString("                | |/ _ \\_  /_  /| ___ \\/ _` | | |");
		do Output.println();
		do Output.printString("            /\\__/ /  __// / / / | |_/ / (_| | | |");
		do Output.println();
		do Output.printString("            \\____/ \\___/___/___|\\____/ \\__,_|_|_|");
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.println();
		do Output.printString("          Use the arrow keys to move the cursor.");
		do Output.println();
		do Output.printString("          Use the A key to create a vertical wall.");
		do Output.println();
		do Output.printString("          Use the S key to create a horizontal wall.");
		do Output.println();
		do Output.println();
		do Output.printString("          Press any key to start game.");

		while (key = 0) {
			let key = Keyboard.keyPressed();
		}

		let key = 82;

		while (key = 82) {
			do run();
			do Sys.wait(1000);
			do drawOverlay();
			do Output.moveCursor(8, 28);
			do Output.printString("Game over");
			do Output.moveCursor(12, 25);
			do Output.printString("Final level: ");
			do Output.printInt(level_num);
			do Output.moveCursor(15, 19);
			do Output.printString("Press R to replay, Q to quit");

			let key = 0;

			while (~((key = 82) | (key = 81))) {
				let key = Keyboard.keyPressed();
			}
		}

		return;
	}

	method void run() {
		var char key;
		var boolean won;

		do Display.setOffsets(6, 14);
		do Display.setSize(50, 24);

		let level_num = 0;
		let won = true;
		while (won) {
			let level_num = level_num + 1;
			let level = Level.new(level_num);
			do level.play();

			let won = level.won();
			do level.dispose();
			let level = null;

			if (won) {
				do Sys.wait(1000);
				do drawOverlay();
				do Output.moveCursor(10, 24);
				do Output.printString("Level ");
				do Output.printInt(level_num);
				do Output.printString(" complete.");
				do Output.moveCursor(14, 20);
				do Output.printString("Press any key to continue");

				let key = 0;

				while (key = 0) {
					let key = Keyboard.keyPressed();
				}
			}
		}

		return;
	}

	method void drawOverlay() {
		do Screen.setColor(true);
		do Screen.drawRectangle(50, 50, 461, 205);
		do Screen.setColor(false);
		do Screen.drawRectangle(51, 51, 460, 204);
		return;
	}
}