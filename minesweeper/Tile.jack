class Tile {
    field boolean _isMine;
    field boolean isRevealed, _isFlag;
    field int x, y;
    field Board board;
    
    constructor Tile new(int _x, int _y, Board _board) {
        let x = _x;
        let y = _y;
        let _isMine = false;
        let isRevealed = false;
        let board = _board;
        let _isFlag = false;
        return this;
    }
    
    method void setIsMine(boolean value) {
        let _isMine = value;
        return;
    }
    
    method void reveal() {
        if (~isRevealed) {
            let isRevealed = true;
            
            if (_isFlag) {
                do toggleIsFlag();
            }
            
            do Drawing.drawTileValue(this);
            do board.decrementUnrevealedTileCount();
        }
        
        return;
    }
    
    method int getX() { return x; }
    method int getY() { return y; }
    method boolean isMine() { return _isMine; }
    method boolean isFlag() { return _isFlag; }
    method boolean hasBeenRevealed() { return isRevealed; }
    method Board getBoard() { return board; }
    
    method void toggleIsFlag() {
        let _isFlag = ~_isFlag;

        do Drawing.toggleFlag(_isFlag, x, y);
        
        return;
    }
}